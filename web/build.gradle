apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

war {
    baseName = 'docgen' // leads to docgen.war
}

dependencies {

    compile 'log4j:log4j:1.2.17',
            'org.springframework:spring-webmvc:3.1.2.RELEASE',
            'org.apache.tiles:tiles-core:2.1.4',
            'org.apache.tiles:tiles-jsp:2.1.4',
            'jstl:jstl:1.2'

    testCompile 'org.mockito:mockito-core:1.9.0',
                'junit:junit:4.+'

    integrationTestCompile sourceSets.main.output
    integrationTestCompile configurations.testCompile
    integrationTestCompile sourceSets.test.output
    integrationTestRuntime configurations.testRuntime

}

task integrationTest(type: Test, dependsOn: jar) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath
}

check.dependsOn integrationTest
